description 'enjoy-diving common module project'

jar {
    baseName = 'enjoy-diving-common'
    version = '0.0.1-SNAPSHOT'
}

dependencies {
    ext {
        springBootVersion = '1.5.2.RELEASE'
        queryDslVersion = '4.1.4'
        springSessionVersion = '1.3.1.RELEASE'
        springSocialVersion = '1.1.4.RELEASE'
        springSocialFacebookVersion = '2.0.3.RELEASE'
        queryDslVersion = '4.1.4'
    }

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: "${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis', version: "${springBootVersion}"
    
    compile group: 'org.bgee.log4jdbc-log4j2', name: 'log4jdbc-log4j2-jdbc4.1', version: '1.16'
    
    compile group: 'org.springframework.social', name: 'spring-social-security', version: "${springSocialVersion}"
    compile group: 'org.springframework.social', name: 'spring-social-config', version: "${springSocialVersion}"
    compile group: 'org.springframework.social', name: 'spring-social-facebook', version: "${springSocialFacebookVersion}"
    
    compile group: 'com.querydsl', name: 'querydsl-core', version: queryDslVersion
    compile group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
    compile group: 'com.querydsl', name: 'querydsl-apt', version: queryDslVersion
    
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.41'
    compile group: 'com.h2database', name: 'h2', version: '1.4.194'
    
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
}

eclipse {
    classpath {
        downloadSources = true
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers('org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8')
    }
}

/* queryDsl setup */
def queryDslOutput = file("src-gen/main/java")
 
sourceSets {
    main {
        java {
            srcDir "src-gen/main/java"
        }
    }
}
 
task generateQueryDSL(type: JavaCompile, group: 'build') {
    doFirst {
        if (!queryDslOutput.exists()) {
            logger.info("Creating `$queryDslOutput` directory")
 
            if (!queryDslOutput.mkdirs()) {
                throw new InvalidUserDataException("Unable to create `$queryDslOutput` directory")
            }
        }
    }
 
    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor",
            "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = queryDslOutput
}
 
compileTestJava.dependsOn(generateQueryDSL)

clean {
    delete queryDslOutput
}

bootRepackage {
    enabled = false
}